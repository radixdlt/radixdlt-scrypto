## NOTE: This manifest is not intended to make cohesive sense
## - it is more intended to demonstrate various authzone-related commands 

# Withdraw XRD from account
CALL_METHOD Address("${account_address}") "withdraw" Address("${xrd_resource_address}") Decimal("5.0");

# Create a proof from bucket, clone it and drop both
TAKE_ALL_FROM_WORKTOP Address("${xrd_resource_address}") Bucket("some_xrd");
CREATE_PROOF_FROM_BUCKET_OF_AMOUNT Bucket("some_xrd") Decimal("1") Proof("proof1a");
CREATE_PROOF_FROM_BUCKET_OF_NON_FUNGIBLES Bucket("some_xrd") Array<NonFungibleLocalId>(NonFungibleLocalId("#123#")) Proof("proof1b");
CREATE_PROOF_FROM_BUCKET_OF_ALL Bucket("some_xrd") Proof("proof1c");
CLONE_PROOF Proof("proof1c") Proof("proof1d");
DROP_PROOF Proof("proof1d");
DROP_PROOF Proof("proof1c");
DROP_AUTH_ZONE_PROOFS;

# Create a proof from account, pop it and push it back
CALL_METHOD Address("${account_address}") "create_proof_of_amount" Address("${resource_address}") Decimal("5.0");
POP_FROM_AUTH_ZONE Proof("proof3");
PUSH_TO_AUTH_ZONE Proof("proof3");

# Compose proofs
CALL_METHOD Address("${account_address}") "create_proof_of_amount" Address("${resource_address}") Decimal("5.0");
CREATE_PROOF_FROM_AUTH_ZONE_OF_AMOUNT Address("${resource_address}") Decimal("1") Proof("proof4");
CREATE_PROOF_FROM_AUTH_ZONE_OF_NON_FUNGIBLES Address("${non_fungible_resource_address}") Array<NonFungibleLocalId>(NonFungibleLocalId("#123#")) Proof("proof5");
CREATE_PROOF_FROM_AUTH_ZONE_OF_ALL Address("${non_fungible_resource_address}") Proof("proof6");

# Drop all signature proofs in the auth zone.
DROP_AUTH_ZONE_SIGNATURE_PROOFS;

# Drop all regular proofs in the auth zone.
DROP_AUTH_ZONE_REGULAR_PROOFS;

# Drop all proofs in the auth zone.
DROP_AUTH_ZONE_PROOFS;

# Drop all proofs
DROP_NAMED_PROOFS;

# Drop all proofs
DROP_ALL_PROOFS;

CALL_METHOD Address("${account_address}") "deposit_batch" Expression("ENTIRE_WORKTOP");
