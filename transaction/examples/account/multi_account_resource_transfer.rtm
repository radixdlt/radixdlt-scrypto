# This transaction shows how funds can be transferred from one account into multiple accounts all 
# in a single transaction. In this example, imagine that we would like to send XRD to three accounts
# in the following quantities:
# Account A: 150 XRD
# Account B: 130 XRD
# Account C: 50 XRD
# Which is a total of 330 XRD. 

# ==================================================================================================
# WARNING: If you will be submitting this transaction through the Babylon wallet then you MUST
# remove the "lock_fee" instruction that you see below. Otherwise your transaction will fail. If all 
# that you are using is resim then you can safely ignore this warning.
# ==================================================================================================
CALL_METHOD 
    Address("${this_account_address}") 
    "lock_fee"
    Decimal("10");

# Withdrawing 330 XRD from the account component
CALL_METHOD 
    Address("${this_account_address}") 
    "withdraw"
    Address("${xrd_resource_address}")
    Decimal("330");

# Taking 150 XRD from the worktop and depositing them into Account A
TAKE_FROM_WORKTOP_BY_AMOUNT
    Decimal("150")
    Address("${xrd_resource_address}")
    Bucket("account_a_bucket");
CALL_METHOD
    Address("${account_a_component_address}")
    "deposit"
    Bucket("account_a_bucket");

# Taking 130 XRD from the worktop and depositing them into Account B
TAKE_FROM_WORKTOP_BY_AMOUNT
    Decimal("130")
    Address("${xrd_resource_address}")
    Bucket("account_b_bucket");
CALL_METHOD
    Address("${account_b_component_address}")
    "deposit"
    Bucket("account_b_bucket");

# Taking 50 XRD from the worktop and depositing them into Account C
TAKE_FROM_WORKTOP_BY_AMOUNT
    Decimal("50")
    Address("${xrd_resource_address}")
    Bucket("account_c_bucket");
CALL_METHOD
    Address("${account_c_component_address}")
    "deposit"
    Bucket("account_c_bucket");