CALL_METHOD ComponentAddress("${account}") "lock_fee" Decimal("10");

# Prepare - grab token from the `Hello` component
CALL_METHOD ComponentAddress("${component}") "free_token";

# Test - assertions
ASSERT_WORKTOP_CONTAINS ResourceAddress("${resource}");
ASSERT_WORKTOP_CONTAINS_BY_AMOUNT Decimal("1.0") ResourceAddress("${resource}");

# Test - worktop
TAKE_FROM_WORKTOP ResourceAddress("${resource}") Bucket("bucket1");
RETURN_TO_WORKTOP Bucket("bucket1");
TAKE_FROM_WORKTOP_BY_AMOUNT Decimal("1.0") ResourceAddress("${resource}") Bucket("bucket2");
RETURN_TO_WORKTOP Bucket("bucket2");

# Test - auth zone
CALL_METHOD ComponentAddress("${account}") "create_proof_by_amount" Decimal("5.0") ResourceAddress("${xrd}");
CREATE_PROOF_FROM_AUTH_ZONE ResourceAddress("${xrd}") Proof("proof1");
CREATE_PROOF_FROM_AUTH_ZONE_BY_AMOUNT Decimal("2.0") ResourceAddress("${xrd}") Proof("proof2");
CLONE_PROOF Proof("proof2") Proof("proof3");
DROP_PROOF Proof("proof1");
DROP_PROOF Proof("proof2");
DROP_PROOF Proof("proof3");

# Test - bucket proof
CALL_METHOD ComponentAddress("${account}") "withdraw_by_amount" Decimal("5.0") ResourceAddress("${xrd}");
CLEAR_AUTH_ZONE;
TAKE_FROM_WORKTOP ResourceAddress("${xrd}") Bucket("xrd");
CREATE_PROOF_FROM_BUCKET Bucket("xrd") Proof("proof4");
CLONE_PROOF Proof("proof4") Proof("proof5");
DROP_PROOF Proof("proof4");
DROP_PROOF Proof("proof5");
RETURN_TO_WORKTOP Bucket("xrd");

# Clean up - deposit resources
CALL_METHOD ComponentAddress("${account}") "deposit_batch" Expression("ENTIRE_WORKTOP");
